apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'

android {
    // https://stackoverflow.com/questions/18328730/how-to-create-a-release-signed-apk-file-using-gradle
    signingConfigs {
        release {
            keyAlias "ipolyglot"
            storeFile file("$rootProject.projectDir/ipolyglot.jks")
            keyPassword "android"
            storePassword "android"
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.kslimweb.ipolyglot"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError true
        absolutePaths false
        disable 'InvalidPackage', 'ContentDescription'
        lintConfig file("$rootProject.projectDir/lint.xml")
    }
    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
            applicationIdSuffix = '.dev'
            firebaseAppDistribution {
                testers="kslim5703@gmail.com"
                serviceCredentialsFile = "$rootProject.projectDir/ipolygot-96fda3515e8a.json"
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            firebaseAppDistribution {
                testers="kslim5703@gmail.com"
                serviceCredentialsFile = "$rootProject.projectDir/ipolygot-96fda3515e8a.json"
            }
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'AlgoliaAppID', ALGOLIA_APP_ID
        it.buildConfigField 'String', 'AlgoliaSearchKey', ALGOLIA_SEARCH_KEY
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/kotlinx-io.kotlin_module'
        pickFirst 'META-INF/atomicfu.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-io.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-core.kotlin_module'
        pickFirst 'META-INF/kotlinx-serialization-runtime.kotlin_module'
        pickFirst 'META-INF/ktor-*'
    }
    applicationVariants.all { variant ->
        variant.outputs.all { v ->
            outputFileName = "ipolygot-${v.baseName}-${variant.versionName}-${defaultConfig.versionCode}.apk"
        }
    }
    dataBinding.enabled = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // mvvm
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // for material design
    implementation 'com.jaredrummler:material-spinner:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'

    // google translation api
    implementation 'com.google.cloud:google-cloud-translate:1.87.0'

    // algolia
    implementation 'com.algolia:instantsearch-android:2.2.1'

    // coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    api 'com.google.dagger:dagger:2.25.3'
    kapt 'com.google.dagger:dagger-compiler:2.22'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.61"

    kapt 'com.github.LunarWatcher:KClassUnpacker:v1.0.1'
    compileOnly "com.github.LunarWatcher:KClassUnpacker:v1.0.1"
    annotationProcessor 'com.github.LunarWatcher:KClassUnpacker:v1.0.1'

//    implementation 'com.github.Omega-R:OmegaRecyclerView:1.9.7@aar' // AndroidX
//    implementation 'com.trading212:sticky-headers:0.1.7'
}